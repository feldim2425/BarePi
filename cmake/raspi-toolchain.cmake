cmake_minimum_required(VERSION 3.3)

set(CMAKE_SYSTEM_NAME Generic)
set(CMAKE_SYSTEM_PROCESSOR ARM)

if(NOT ARM_TOOLCHAIN_TRIPLET)
    set(ARM_TOOLCHAIN_TRIPLET "arm-none-eabi")
    message(STATUS "No ARM_TOOLCHAIN_TRIPLET specified, using default: " ${ARM_TOOLCHAIN_TRIPLET})
endif()

set(CMAKE_C_COMPILER "${ARM_TOOLCHAIN_TRIPLET}-gcc")
set(CMAKE_ASM_COMPILER "${CMAKE_C_COMPILER}")
set(CMAKE_CXX_COMPILER "${ARM_TOOLCHAIN_TRIPLET}-g++")
set(CMAKE_OBJCOPY "${ARM_TOOLCHAIN_TRIPLET}-objcopy")
set(CMAKE_OBJDUMP "${ARM_TOOLCHAIN_TRIPLET}-objdump")
set(CMAKE_SIZE    "${ARM_TOOLCHAIN_TRIPLET}-size")
set(CMAKE_CPPFILT "${ARM_TOOLCHAIN_TRIPLET}-c++filt")

set(ARM_TOOLCHAIN_FLAGS "-mcpu=arm1176jz-s -marm -mfpu=vfp -mthumb -ffreestanding -nostdlib -nostartfiles")

set(ARM_LINKER_SCRIPT "${CMAKE_CURRENT_LIST_DIR}/../barepi.ld")

set(CMAKE_C_FLAGS "${ARM_TOOLCHAIN_FLAGS}")
set(CMAKE_CXX_FLAGS "${ARM_TOOLCHAIN_FLAGS}")
set(CMAKE_ASM_FLAGS "${ARM_TOOLCHAIN_FLAGS}")
set(CMAKE_EXE_LINKER_FLAGS "${ARM_TOOLCHAIN_FLAGS} -T ${ARM_LINKER_SCRIPT}")
set(CMAKE_EXECUTABLE_SUFFIX_CXX ".elf")
set(CMAKE_EXECUTABLE_SUFFIX_C ".elf")

set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)

function(rpi_add_executable TARGET)
    add_executable(${TARGET} ${ARGN})
    set_target_properties(${TARGET} PROPERTIES LINK_DEPENDS ${ARM_LINKER_SCRIPT})
    add_custom_target(${TARGET}.img ALL DEPENDS ${TARGET} COMMAND ${CMAKE_OBJCOPY} $<TARGET_FILE:${TARGET}> -O binary $<TARGET_FILE_DIR:${TARGET}>/${TARGET}.img)
endfunction()
